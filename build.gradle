buildscript {
    dependencies {
        classpath 'org.kohsuke:github-api:1.306'
    }
}

plugins {
    id "fabric-loom" version "0.12.+"
    id "maven-publish"
    id "org.ajoberstar.grgit" version "3.1.0"
    id "com.matthewprenger.cursegradle" version "1.4.0"
    id "com.modrinth.minotaur" version "2.+"
}

loom {
    accessWidenerPath = file("src/main/resources/autowhitelist.accesswidener")
}

repositories {
    mavenCentral()
    maven {
      name 'm2-dv8tion'
      url 'https://m2.dv8tion.net/releases'
    }
}

def ENV = System.getenv()

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

configurations {
    includeTransitive
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    include(modImplementation(fabricApi.module("fabric-lifecycle-events-v1", project.fabric_version)))
    include(modImplementation(fabricApi.module("fabric-api-base", project.fabric_version)))

    implementation('net.dv8tion:JDA:4.4.0') {
        exclude module: 'opus-java'
    }

    includeTransitive implementation('net.dv8tion:JDA:4.4.0_352') {
        exclude module: 'opus-java'
    }
}

configurations.getByName("includeTransitive").resolvedConfiguration.resolvedArtifacts.forEach(dep -> {
    if (!dep.id.componentIdentifier.displayName.contains("slf4j-api"))
        dependencies.add("include", dep.id.componentIdentifier.displayName)
})

compileJava {
    sourceCompatibility = 17
    targetCompatibility = 17
    options.encoding = 'UTF-8'
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
/*task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}*/

jar {
    from "LICENSE"
}

def getBranch() {
    def ENV = System.getenv()
    if (ENV.GITHUB_REF) {
        def branch = ENV.GITHUB_REF
        return branch.substring(branch.lastIndexOf("/") + 1)
    }

    if (grgit == null) {
        return "unknown"
    }

    def branch = grgit.branch.current().name
    return branch.substring(branch.lastIndexOf("/") + 1)
}

task remapMavenJar(type: net.fabricmc.loom.task.RemapJarTask, dependsOn: jar) {
    input = jar.archiveFile
    archiveFileName = "${archivesBaseName}-${project.version}-maven.jar"
    addNestedDependencies = false
}
build.dependsOn remapMavenJar

curseforge {
    if (ENV.CURSEFORGE_API_KEY) {
        apiKey = ENV.CURSEFORGE_API_KEY
    }

    project {
        id = "575422"
        changelog = ENV.CHANGELOG ?: "No changelog provided"
        releaseType = "release"
        addGameVersion "$project.minecraft_version"
        addGameVersion "Fabric"

        mainArtifact(remapJar) {
            displayName = "[$project.supported_versions] AutoWhitelist - $project.version"
        }

        afterEvaluate {
            uploadTask.dependsOn("remapJar")
        }
    }

    options {
        forgeGradleIntegration = false
    }
}

import org.kohsuke.github.GHReleaseBuilder
import org.kohsuke.github.GitHub

task github(dependsOn: remapMavenJar) {
    onlyIf {
        ENV.GITHUB_TOKEN
    }

    doLast {
        def github = GitHub.connectUsingOAuth(ENV.GITHUB_TOKEN as String)
        def repository = github.getRepository(ENV.GITHUB_REPOSITORY)

        def releaseBuilder = new GHReleaseBuilder(repository, version as String)
        releaseBuilder.name("[$project.supported_versions] AutoWhitelist - $project.version")
        releaseBuilder.body(ENV.CHANGELOG ?: "No changelog provided")
        releaseBuilder.commitish(getBranch())

        def ghRelease = releaseBuilder.create()
        ghRelease.uploadAsset(file("${project.buildDir}/libs/${archivesBaseName}-${version}.jar"), "application/java-archive");
    }
}

modrinth {
    projectId = "2tvyop0z"
    versionName = "[$project.supported_versions] AutoWhitelist - $project.version"
    changelog = ENV.CHANGELOG ?: "No changelog provided"
    uploadFile = remapJar
}